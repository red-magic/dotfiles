_pkgname=en-croissant
pkgname=${_pkgname}-appimage
pkgver=0.11.1
pkgrel=1
pkgdesc='The Ultimate Chess Toolkit'
url=https://github.com/franciscoBSalgueiro/${_pkgname}
arch=(x86_64)
depends=(fuse2 zlib hicolor-icon-theme)
conflicts=(en-croissant en-croissant-bin)
license=(GPL-3.0)
options=(!strip !debug)
optdepends=(
	'noto-fonts: UTF-8 chess symbols support'
	'gnu-free-fonts: UTF-8 chess symbols support'
	'ttf-dejavu: UTF-8 chess symbols support'
)

_appimage=${_pkgname}_${pkgver}_amd64.AppImage

source=(
	$url/releases/download/v${pkgver}/${_appimage}
	https://raw.githubusercontent.com/franciscoBSalgueiro/${_pkgname}/refs/tags/v${pkgver}/LICENSE
)

b2sums=(
	20d7195afcab2685cc92f161b0d4aa50b80eca9742518e14043dbf3b033ae844fe5d6797056c62c19fe2c95d235285321a965abf04c25bc803a419c10c467b76
	74915e048cf8b5207abf603136e7d5fcf5b8ad512cce78a2ebe3c88fc3150155893bf9824e6ed6a86414bbe4511a6bd4a42e8ec643c63353dc8eea4a44a021cd
)

prepare(){
	chmod 755 ./${_appimage}
	./${_appimage} --appimage-extract

	sed -i 's/Name=en-croissant/Name=En Croissant/' squashfs-root/usr/share/applications/${_pkgname}.desktop
}

package() {
	install -Dm 755 ${_appimage} $pkgdir/opt/${_pkgname}/${_appimage}

	install -dm 755 $pkgdir/usr/bin
	ln -sfv /opt/${_pkgname}/${_appimage} $pkgdir/usr/bin/${_pkgname}

	install -Dm 644 squashfs-root/usr/share/applications/${_pkgname}.desktop -t $pkgdir/usr/share/applications
	install -Dm 644 squashfs-root/usr/share/icons/hicolor/32x32/apps/${_pkgname}.png -t $pkgdir/usr/share/icons/hicolor/32x32/apps
	install -Dm 644 squashfs-root/usr/share/icons/hicolor/128x128/apps/${_pkgname}.png -t $pkgdir/usr/share/icons/hicolor/128x128/apps
	install -Dm 644 squashfs-root/usr/share/icons/hicolor/256x256@2/apps/${_pkgname}.png -t $pkgdir/usr/share/icons/hicolor/256x256@2/apps

	install -Dm 644 LICENSE -t $pkgdir/usr/share/licenses/${_pkgname}
}
