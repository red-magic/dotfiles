pkgname=linux-znver3
pkgver=6.9.1
_archver=1
pkgrel=1
pkgdesc='Linux kernel for znver3 architecture'
arch=(x86_64)
license=(GPL2)
depends=(base-devel
	 bc
	 cpio
	 pahole
	 #inetutils
	)
options=(!debug)
source=(https://cdn.kernel.org/pub/linux/kernel/v${pkgver::1}.x/linux-$pkgver.tar.xz
	https://gitlab.archlinux.org/archlinux/packaging/packages/linux/-/raw/$pkgver.arch$_archver-$pkgrel/config)
b2sums=(SKIP
	SKIP)

_linuxsrc=linux-$pkgver

_kernelmake() {
	_ncpu=$(($(nproc)+1))
	_arch=znver3
	_flags="-O2 -pipe -march=$_arch -mtune=$_arch"
	make -j$_ncpu LOCALVERSION=-arch$_archver-$pkgrel-$_arch KCFLAGS="$_flags" KCPPFLAGS="$_flags" "$@"
}

prepare() {
	cd $_linuxsrc
	_kernelmake mrproper
	cp -f $srcdir/config $srcdir/$_linuxsrc/.config
	_kernelmake prepare
}

build() {
	cd $_linuxsrc
	_kernelmake bzImage modules
	_kernelmake -s kernelrelease > version
}

package() {
	cd $srcdir/$_linuxsrc
	ZSTD_CLEVEL=19 _kernelmake INSTALL_MOD_STRIP=1 INSTALL_MOD_PATH=$pkgdir/usr modules_install
	rm -f $pkgdir/usr/lib/modules/"$(<version)"/{build,source}
	install -Dm 644 $srcdir/$_linuxsrc/arch/x86_64/boot/bzImage $pkgdir/boot/vmlinuz-$pkgname
	install -Dm 644 $srcdir/$_linuxsrc/arch/x86_64/boot/bzImage $pkgdir/usr/lib/modules/"$(<version)"/vmlinuz
	echo $pkgname > $pkgdir/usr/lib/modules/"$(<version)"/pkgbase
}
